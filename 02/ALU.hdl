// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

//BASICAMENTE TENEMOS DOS VALORES, X Y Y 

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

     
    PARTS:
    // --- PROCESAMIENTO DE X ---
    // zx: Si zx=1, ponemos x a 0
    Mux16(a=x, b=false, sel=zx, out=x1);
    // nx: Si nx=1, negamos x1 (Not x)
    Not16(in=x1, out=notx1);
    Mux16(a=x1, b=notx1, sel=nx, out=x2);

    // --- PROCESAMIENTO DE Y ---
    // zy: Si zy=1, ponemos y a 0
    Mux16(a=y, b=false, sel=zy, out=y1);
    // ny: Si ny=1, negamos y1 (Not y)
    Not16(in=y1, out=noty1);
    Mux16(a=y1, b=noty1, sel=ny, out=y2);

    // --- SELECCION DE FUNCIÓN (f) ---
    // f: Si f=1 suma (x2 + y2), si f=0 hace AND (x2 & y2)
    Add16(a=x2, b=y2, out=sumXY);
    And16(a=x2, b=y2, out=andXY);
    Mux16(a=andXY, b=sumXY, sel=f, out=fOut);

    // --- PROCESAMIENTO DE SALIDA (no) Y FLAGS ---
    // no: Si no=1, negamos el resultado final
    Not16(in=fOut, out=notFOut);
    
    //  sacamos el bit 15 para 'ng' 
    // y dividimos la salida en dos partes de 8 bits para calcular 'zr' y utilizar el 8 way en vez del or normal
    Mux16(a=fOut, b=notFOut, sel=no, out=out, out[15]=ng, out[0..7]=lowPart, out[8..15]=highPart);

    // --- CALCULO DE ZR (¿Es cero?) ---
    // Si cualquier bit es 1, Or8Way devolvera 1. 
    // Si después de pasar por ambos Or8Way y el Or final obtenemos 0, es que todo era 0.
    Or8Way(in=lowPart, out=orLow);
    Or8Way(in=highPart, out=orHigh);
    Or(a=orLow, b=orHigh, out=notZr);
    Not(in=notZr, out=zr);
}
    